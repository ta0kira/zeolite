/* -----------------------------------------------------------------------------
Copyright 2023 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "CheckAlways" {
  success
}

unittest match {
  TestReportTree report <- TestReportTree.new("title",ValueList<Formatted>.new())
  \ report.addError(message: "message")
  \ CheckAlways:match().check(123,report)
  \ Testing.checkFalse(report.hasError())
}

unittest fromWithoutError {
  TestReportTree report <- TestReportTree.new("title",ValueList<Formatted>.new())
  \ CheckAlways:error("message").check(123,report)
  \ Testing.checkTrue(report.hasError())
}


testcase "CheckAlways:die" {
  failure
  require "something"
}

unittest test {
  TestReportTree report <- TestReportTree.new("title",ValueList<Formatted>.new())
  \ report.addError(message: "message")
  \ CheckAlways:die("something").check(123,report)
}


testcase "MultiChecker.check short cicuits" {
  failure
  require "title0"
  require "title2"
  require "message2"
  exclude "title1"
  exclude "title3"
  exclude "message3"
}

unittest test {
  TestReportTree report <- TestReportTree.new("title0",ValueList<Formatted>.new())

  \ MultiChecker.new(report)
      .check<String>(title: "title1","foo",CheckAlways:match())
      &.check<String>(title: "title2","foo",CheckAlways:error("message2"))
      &.check<String>(title: "title3","foo",CheckAlways:error("message3"))

  IndentAppend output <- IndentAppend.new(String.builder())
  \ report.writeTo(output)
  \ TestHandler:failAndExit(output.build().formatted())
}


testcase "MultiChecker.tryCheck continues" {
  failure
  require "title0"
  require "title2"
  require "message2"
  require "title3"
  require "message3"
  exclude "title1"
}

unittest test {
  TestReportTree report <- TestReportTree.new("title0",ValueList<Formatted>.new())

  \ MultiChecker.new(report)
      .tryCheck<String>(title: "title1","foo",CheckAlways:match())
      .tryCheck<String>(title: "title2","foo",CheckAlways:error("message2"))
      .tryCheck<String>(title: "title3","foo",CheckAlways:error("message3"))

  IndentAppend output <- IndentAppend.new(String.builder())
  \ report.writeTo(output)
  \ TestHandler:failAndExit(output.build().formatted())
}


testcase "MultiChecker.check doesn't force pending errors" {
  failure
  require "title0"
  require "title1"
  require "message1"
  require "title3"
  require "message3"
  exclude "title2"
}

unittest test {
  TestReportTree report <- TestReportTree.new("title0",ValueList<Formatted>.new())

  \ MultiChecker.new(report)
      .tryCheck<String>(title: "title1","foo",CheckAlways:error("message1"))
      .check<String>(title: "title2","foo",CheckAlways:match())
      &.tryCheck<String>(title: "title3","foo",CheckAlways:error("message3"))

  IndentAppend output <- IndentAppend.new(String.builder())
  \ report.writeTo(output)
  \ TestHandler:failAndExit(output.build().formatted())
}


testcase "CheckValue:equals success" {
  success TestChecker
}

unittest nonEmpty {
  \ "foo" `Matches:with` CheckValue:equals("foo")
}

unittest bothEmpty {
  \ empty?String `Matches:with` CheckValue:equals(empty?String)
}


testcase "CheckValue:equals failure not equal" {
  failure TestChecker
  require "foo"
  require "bar"
}

unittest test {
  \ "foo" `Matches:with` CheckValue:equals("bar")
}


testcase "CheckValue:equals failure left empty" {
  failure TestChecker
  require "empty"
  require "bar"
}

unittest test {
  \ empty?String `Matches:with` CheckValue:equals("bar")
}


testcase "CheckValue:equals failure right empty" {
  failure TestChecker
  require "foo"
  require "empty"
}

unittest test {
  \ "foo" `Matches:with` CheckValue:equals(empty?String)
}


testcase "CheckValue:notEquals success" {
  success TestChecker
}

unittest nonEmpty {
  \ "foo" `Matches:with` CheckValue:notEquals("bar")
}

unittest leftEmpty {
  \ empty?String `Matches:with` CheckValue:notEquals("bar")
}

unittest rightEmpty {
  \ "foo" `Matches:with` CheckValue:notEquals(empty?String)
}


testcase "CheckValue:notEquals failure non-empty" {
  failure TestChecker
  require "foo"
}

unittest test {
  \ "foo" `Matches:with` CheckValue:notEquals("foo")
}


testcase "CheckValue:notEquals failure empty" {
  failure TestChecker
  require "empty"
}

unittest test {
  \ empty?String `Matches:with` CheckValue:notEquals(empty?String)
}


testcase "CheckValue:from success" {
  success TestChecker
}

unittest test {
  \ TestValue.default() `Matches:with` `CheckValue:from` TestValue.default()
}


testcase "CheckValue:from failure not equal" {
  failure TestChecker
  require "message1"
}

unittest test {
  \ TestValue.default() `Matches:with` `CheckValue:from` TestValue.new(123,"message1",empty)
}


testcase "CheckValue:from failure left empty" {
  failure TestChecker
  require "empty"
}

unittest test {
  \ empty?TestValue `Matches:with` `CheckValue:from` TestValue.new(123,"message1",empty)
}
