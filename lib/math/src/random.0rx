/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define Randomize {
  permuteFrom (tree,random,output) {
    CategoricalTree<#c> copy <- tree.duplicate()
    Generator<#c> generator <- copy `RandomCategorical:sampleWith` random
    $Hidden[tree,random]$
    while (copy.getTotal() > 0) {
      #c cat <- generator.generate()
      \ output.append(cat)
      \ copy.decrWeight(cat)
    }
  }

  permuteFromWeight (tree,random,output) {
    CategoricalTree<#c> copy <- tree.duplicate()
    Generator<#c> generator <- copy `RandomCategorical:sampleWith` random
    $Hidden[tree,random]$
    while (copy.getTotal() > 0) {
      #c cat <- generator.generate()
      \ output.append(cat)
      \ copy.setWeight(cat,0)
    }
  }
}

define GenerateConstant {
  $ReadOnly[constant]$

  @value Float constant

  new (constant) {
    return GenerateConstant{ constant }
  }

  generate () {
    return constant
  }
}

define RandomCategorical {
  $ReadOnly[categorical,random]$

  @value CategoricalReader<#c> categorical
  @value Generator<Float> random

  sampleWith (categorical,random) {
    return RandomCategorical<#c>{ categorical, random }
  }

  generate () {
    return categorical.locate((categorical.getTotal().asFloat()*random.generate()).asInt())
  }

  isEmpty () {
    return categorical.getTotal() == 0
  }
}
