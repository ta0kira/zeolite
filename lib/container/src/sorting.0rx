/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define Sorting {
  sort (seq) {
    \ sortWith<#x,#x>(seq)
  }

  sortWith (seq) {
    \ HeapSort<#x,#xx>.inPlace(seq)
  }

  reverse (seq) {
    scoped {
      Int i <- seq.size()/2-1
    } in while (i >= 0) {
      $ReadOnly[i]$
      Int j <- seq.size()-i-1
      #x temp <- seq.readAt(i)
      \ seq.writeAt(i,seq.readAt(j))
      \ seq.writeAt(j,temp)
    } update {
      i <- i-1
    }
  }

  sortList (head) {
    return sortListWith<#n,#x,#x>(head)
  }

  sortListWith (head) {
    optional #n oldPrev <- empty
    return MergeSort<#n,#x,#xx>.sort(head)
  }

  reverseList (head) (head2) {
    if (!present(head)) {
      return empty
    }
    head2 <- head
    $Hidden[head]$

    scoped {
      optional #n prev <- empty
    } in while (true) {
      optional #n next <- require(head2).next()
      \ require(head2).setNext(prev)
      prev <- head2
      if (present(next)) {
        head2 <- next
      } else {
        break
      }
    }
  }
}

// Putting the params at the top level allows helpers to be called without
// needing to pass the params every time.
concrete HeapSort<#x,#xx> {
  #xx defines LessThan<#x>

  @type inPlace ([ReadAt<#x>&WriteAt<#x>]) -> ()
}

define HeapSort {
  $ReadOnly[seq]$

  @value [ReadAt<#x>&WriteAt<#x>] seq

  inPlace (seq) {
    \ #self{ seq }.execute()
  }

  @value execute () -> ()
  execute () { $NoTrace$
    // Convert the container to a heap.
    scoped {
      Int i <- seq.size()/2-1
    } in while (i >= 0) {
      $ReadOnly[i]$
      \ sift(i,seq.size())
    } update {
      i <- i-1
    }

    // Traverse the heap and populate the container in place.
    scoped {
      Int i <- seq.size()-1
    } in while (i >= 0) {
      $ReadOnly[i]$
      \ swap(0,i)
      \ sift(0,i)
    } update {
      i <- i-1
    }
  }

  @value sift (Int,Int) -> ()
  sift (start,size) { $NoTrace$
    scoped {
      Int last         <- start
      Int indexLargest <- last
      $Hidden[start]$
    } in while (2*last+1 < size) {
      $ReadOnly[last]$
      Int left  <- 2*last+1
      Int right <- 2*last+2
      $ReadOnly[left,right]$
      if (seq.readAt(indexLargest) `#xx.lessThan` seq.readAt(left)) {
        indexLargest <- left
      }
      if (right < size && seq.readAt(indexLargest) `#xx.lessThan` seq.readAt(right)) {
        indexLargest <- right
      }
      if (indexLargest == last) {
        break
      }
    } update {
      \ swap(last,indexLargest)
      last <- indexLargest
    }
  }

  @value swap (Int,Int) -> ()
  swap (i,j) { $NoTrace$
    if (i != j) {
      #x temp <- seq.readAt(i)
      \ seq.writeAt(i,seq.readAt(j))
      \ seq.writeAt(j,temp)
    }
  }
}

// Putting the params at the top level allows helpers to be called without
// needing to pass the params every time.
concrete MergeSort<#n,#x,#xx> {
  #xx defines LessThan<#x>
  #n requires ListNode<#n,#x>

  @type sort (optional #n) -> (optional #n)
}

define MergeSort {
  sort (head) {
    if (!present(head) || !present(require(head).next())) {
      return head
    }
    optional #n middle <- bisect(head)
    return merge(sort(head),sort(middle))
  }

  @type bisect (optional #n) -> (optional #n)
  bisect (head) {
    optional #n middle <- head

    scoped {
      optional #n front <- head
      $Hidden[head]$
    } in while (present(front)) {
      $ReadOnly[middle]$
      if (!present((front <- require(front).next())) ||
          !present((front <- require(front).next()))) {
        break
      }
    } update {
      middle <- require(middle).next()
    }

    cleanup {
      \ require(middle).setNext(empty)
    } in return require(middle).next()
  }

  @type merge (optional #n,optional #n) -> (optional #n)
  merge (left,right) (head) {
    head <- empty
    optional #n tail <- empty

    optional #n left2  <- left
    optional #n right2 <- right
    $Hidden[left,right]$

    while (present(left2) && present(right2)) {
      #n append <- defer
      if (require(left2).get() `#xx.lessThan` require(right2).get()) {
        append <- require(left2)
        left2 <- append.next()
        \ append.setNext(empty)
      } else {
        append <- require(right2)
        right2 <- append.next()
        \ append.setNext(empty)
      }
      if (!present(head)) {
        head <- append
      } else {
        \ require(tail).setNext(append)
      }
      tail <- append
    }

    if (present(left2)) {
      if (!present(head)) {
        head <- require(left2)
      } else {
        \ require(tail).setNext(require(left2))
      }
    }

    if (present(right2)) {
      if (!present(head)) {
        head <- require(right2)
      } else {
        \ require(tail).setNext(require(right2))
      }
    }
  }
}
