/* -----------------------------------------------------------------------------
Copyright 2019-2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define AutoBinaryTree {
  @value Int treeSize
  @value optional #n root

  new () {
    return #self{ 0, empty }
  }

  size () {
    return treeSize
  }

  duplicate () {
    if (present(root)) {
      return #self{ treeSize, require(root).duplicate() }
    } else {
      return #self{ 0, empty }
    }
  }

  getRoot () {
    return root
  }

  get (k) {
    return find(root,k)
  }

  set (k,v) {
    root, Bool added <- insert(root,k,v)
    if (added) {
      treeSize <- treeSize+1
    }
  }

  weakSet (k,v) {
    root, optional #v v2 <- insertWeak(root,k,v)
    if (`present` v2) {
      return `require` v2
    } else {
      treeSize <- treeSize+1
      return v
    }
  }

  remove (k) {
    root, Bool removed <- delete(root,k)
    if (removed) {
      treeSize <- treeSize-1
    }
  }


  @type insert (optional #n,#k,#v) -> (optional #n,Bool)
  insert (node,k,v) (newRoot,added) {
    if (!present(node)) {
      return #n.newNode(k,v), true
    }
    #n node2 <- require(node)
    if (k `#k.lessThan` node2.getKey()) {
      newRoot, added <- insert(node2.getLower(),k,v)
      \ node2.setLower(newRoot)
      newRoot <- rebalance(node2)
    } elif (node2.getKey() `#k.lessThan` k) {
      newRoot, added <- insert(node2.getHigher(),k,v)
      \ node2.setHigher(newRoot)
      newRoot <- rebalance(node2)
    } else {
      \ node2.setValue(v)
      \ node2.updateNode()
      return node2, false
    }
  }


  @type insertWeak (optional #n,#k,#v) -> (optional #n,optional #v)
  insertWeak (node,k,v) (newRoot,value) {
    if (!present(node)) {
      return #n.newNode(k,v), empty
    }
    #n node2 <- require(node)
    if (k `#k.lessThan` node2.getKey()) {
      newRoot, value <- insertWeak(node2.getLower(),k,v)
      \ node2.setLower(newRoot)
      newRoot <- rebalance(node2)
    } elif (node2.getKey() `#k.lessThan` k) {
      newRoot, value <- insertWeak(node2.getHigher(),k,v)
      \ node2.setHigher(newRoot)
      newRoot <- rebalance(node2)
    } else {
      return node2, node2.getValue()
    }
  }

  @type delete (optional #n,#k) -> (optional #n,Bool)
  delete (node,k) (newRoot,removed) {
    if (!present(node)) {
      return empty, false
    }
    #n node2 <- require(node)
    if (k `#k.lessThan` node2.getKey()) {
      newRoot, removed <- delete(node2.getLower(),k)
      \ node2.setLower(newRoot)
      newRoot <- rebalance(node2)
    } elif (node2.getKey() `#k.lessThan` k) {
      newRoot, removed <- delete(node2.getHigher(),k)
      \ node2.setHigher(newRoot)
      newRoot <- rebalance(node2)
    } else {
      return rebalance(removeNode(node2)), true
    }
  }

  @type find (optional #n,#k) -> (optional #v)
  find (node,k) {
    if (present(node)) {
      scoped {
        #n node2 <- require(node)
      } in if (k `#k.lessThan` node2.getKey()) {
        return find(node2.getLower(),k)
      } elif (node2.getKey() `#k.lessThan` k) {
        return find(node2.getHigher(),k)
      } else {
        return node2.getValue()
      }
    } else {
      return empty
    }
  }

  @type getBalance (optional #n) -> (Int)
  getBalance (node) (balance) {
    balance <- 0
    if (present(node)) {
      #n node2 <- require(node)
      if (present(node2.getLower())) {
        balance <- balance-require(node2.getLower()).getHeight()
      }
      if (present(node2.getHigher())) {
        balance <- balance+require(node2.getHigher()).getHeight()
      }
    }
  }

  @type rebalance (optional #n) -> (optional #n)
  rebalance (node) {
    if (!present(node)) {
      return empty
    }
    #n node2 <- require(node)
    \ node2.updateNode()
    scoped {
      Int balance <- getBalance(node2)
    } in if (balance > 1) {
      return pivotLower(node2)
    } elif (balance < -1) {
      return pivotHigher(node2)
    } else {
      return node2
    }
  }

  @type pivotHigher (#n) -> (#n)
  pivotHigher (node) (newNode) {
    if (getBalance(node.getLower()) > 0) {
      \ node.setLower(pivotLower(require(node.getLower())))
    }
    newNode <- require(node.getLower())
    \ node.setLower(newNode.getHigher())
    \ node.updateNode()
    \ newNode.setHigher(node)
    \ newNode.updateNode()
  }

  @type pivotLower (#n) -> (#n)
  pivotLower (node) (newNode) {
    if (getBalance(node.getHigher()) < 0) {
      \ node.setHigher(pivotHigher(require(node.getHigher())))
    }
    newNode <- require(node.getHigher())
    \ node.setHigher(newNode.getLower())
    \ node.updateNode()
    \ newNode.setLower(node)
    \ newNode.updateNode()
  }

  @type removeNode (#n) -> (optional #n)
  removeNode (node) (newNode) {
    if (getBalance(node) < 0) {
      optional #n temp, newNode <- removeHighest(node.getLower())
      \ node.setLower(temp)
    } else {
      optional #n temp, newNode <- removeLowest(node.getHigher())
      \ node.setHigher(temp)
    }
    if (present(newNode)) {
      \ swapChildren(node,require(newNode))
      \ require(newNode).updateNode()
    }
  }

  @type removeHighest (optional #n) -> (optional #n,optional #n)
  removeHighest (node) (newNode,removed) {
    if (!present(node)) {
      return empty, empty
    }
    #n node2 <- require(node)
    if (present(node2.getHigher())) {
      optional #n temp, removed <- removeHighest(node2.getHigher())
      \ node2.setHigher(temp)
      newNode <- rebalance(node2)
    } else {
      newNode <- node2.getLower()
      \ node2.setLower(empty)
      removed <- node
    }
  }

  @type removeLowest (optional #n) -> (optional #n,optional #n)
  removeLowest (node) (newNode,removed) {
    if (!present(node)) {
      return empty, empty
    }
    #n node2 <- require(node)
    if (present(node2.getLower())) {
      optional #n temp, removed <- removeLowest(node2.getLower())
      \ node2.setLower(temp)
      newNode <- rebalance(node2)
    } else {
      newNode <- node2.getHigher()
      \ node2.setHigher(empty)
      removed <- node
    }
  }

  @type swapChildren (#n,#n) -> ()
  swapChildren (l,r) {
    scoped {
      optional #n temp <- l.getLower()
      \ l.setLower(r.getLower())
    } in \ r.setLower(temp)
    scoped {
      optional #n temp <- l.getHigher()
      \ l.setHigher(r.getHigher())
    } in \ r.setHigher(temp)
  }
}

define SimpleKeyValue {
  $ReadOnly[key,value]$

  refines KeyValue<#k,#v>

  @value #k key
  @value #v value

  create (key,value) {
    return SimpleKeyValue<#k,#v>{ key, value }
  }

  getKey () {
    return key
  }

  getValue () {
    return value
  }
}

define ForwardTreeOrder {
  $ReadOnly[node,prev]$

  @value BinaryTreeNode<#k,#v> node
  @value optional ForwardTreeOrder<#k,#v> prev

  create (node) (current) {
    optional BinaryTreeNode<#k,#v> node2 <- node
    current <- empty
    while (present(node2)) {
      current <- ForwardTreeOrder<#k,#v>{ require(node2), current }
      node2 <- require(node2).getLower()
    }
  }

  seek (key,node) (current) {
    optional BinaryTreeNode<#k,#v> node2 <- node
    current <- empty
    while (present(node2)) {
      if (require(node2).getKey() `#k.lessThan` key) {
        // Skip node2 in the traversal, since it's before key.
        node2 <- require(node2).getHigher()
      } elif (key `#k.lessThan` require(node2).getKey()) {
        current <- ForwardTreeOrder<#k,#v>{ require(node2), current }
        node2 <- require(node2).getLower()
      } else {
        current <- ForwardTreeOrder<#k,#v>{ require(node2), current }
        break
      }
    }
  }

  next () (current) {
    // Algorithm:
    // 1. Pop self from the stack.
    // 2. Traverse lower to the bottom starting from the higher child of self.
    optional BinaryTreeNode<#k,#v> node2 <- node.getHigher()
    current <- prev
    while (present(node2)) {
      current <- ForwardTreeOrder<#k,#v>{ require(node2), current }
      node2 <- require(node2).getLower()
    }
  }

  get () {
    return SimpleKeyValue:create(node.getKey(),node.getValue())
  }
}

define ReverseTreeOrder {
  $ReadOnly[node,prev]$

  @value BinaryTreeNode<#k,#v> node
  @value optional ReverseTreeOrder<#k,#v> prev

  create (node) (current) {
    optional BinaryTreeNode<#k,#v> node2 <- node
    current <- empty
    while (present(node2)) {
      current <- ReverseTreeOrder<#k,#v>{ require(node2), current }
      node2 <- require(node2).getHigher()
    }
  }

  seek (key,node) (current) {
    optional BinaryTreeNode<#k,#v> node2 <- node
    current <- empty
    while (present(node2)) {
      if (require(node2).getKey() `#k.lessThan` key) {
        current <- ReverseTreeOrder<#k,#v>{ require(node2), current }
        node2 <- require(node2).getHigher()
      } elif (key `#k.lessThan` require(node2).getKey()) {
        // Skip node2 in the traversal, since it's after key.
        node2 <- require(node2).getLower()
      } else {
        current <- ReverseTreeOrder<#k,#v>{ require(node2), current }
        break
      }
    }
  }

  next () (current) {
    // Algorithm:
    // 1. Pop self from the stack.
    // 2. Traverse higher to the bottom starting from the lower child of self.
    optional BinaryTreeNode<#k,#v> node2 <- node.getLower()
    current <- prev
    while (present(node2)) {
      current <- ReverseTreeOrder<#k,#v>{ require(node2), current }
      node2 <- require(node2).getHigher()
    }
  }

  get () {
    return SimpleKeyValue:create(node.getKey(),node.getValue())
  }
}
