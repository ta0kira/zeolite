/* -----------------------------------------------------------------------------
Copyright 2020,2022 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "fail builtin" {
  failure
  require stderr "Failed"
  require stderr "failedReturn"
}

unittest test {
  Int value <- Test:failedReturn()
}

concrete Test {
  @category failedReturn () -> (Int)
}

define Test {
  failedReturn () {
    // Calling within a function also validates that return checks are skipped.
    fail("Failed")
  }
}


testcase "wrong type for fail" {
  error
  require "fail"
  require "Formatted"
}

unittest test {
  fail(Value.create())
}

concrete Value {
  @type create () -> (Value)
}

define Value {
  create () {
    return Value{}
  }
}


testcase "exit builtin zero" {
  success
}

unittest test {
  Int value <- Test:exitReturn()
}

concrete Test {
  @category exitReturn () -> (Int)
}

define Test {
  exitReturn () {
    // Calling within a function also validates that return checks are skipped.
    exit(0)
  }
}


testcase "exit builtin non-zero" {
  failure
  // Should not output a stack trace.
  exclude "exitReturn"
}

unittest test {
  Int value <- Test:exitReturn()
}

concrete Test {
  @category exitReturn () -> (Int)
}

define Test {
  exitReturn () {
    exit(1)
  }
}


testcase "wrong type for exit" {
  error
  require "exit"
  require "Int"
}

unittest test {
  exit("message")
}


testcase "require empty" {
  failure
  require stderr "require.+empty"
}

unittest test {
  \ require(empty)
}
