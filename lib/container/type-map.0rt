/* -----------------------------------------------------------------------------
Copyright 2019-2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "TypeMap tests" {
  success
}

unittest basicOperations {
  TypeMap tree <- TypeMap.new()

  TypeKey<Int>    keyInt    <- TypeKey<Int>.new()
  TypeKey<String> keyString <- TypeKey<String>.new()
  TypeKey<Value>  keyValue  <- TypeKey<Value>.new()

  \ Helper.check<?>(tree,keyInt,empty)
  \ Helper.check<?>(tree,keyString,empty)
  \ Helper.check<?>(tree,keyValue,empty)

  \ tree.set<?>(keyInt,1)
  \ tree.set<?>(keyString,"a")
  \ tree.set<?>(keyValue,Value.new())

  \ Helper.check<?>(tree,keyInt,1)
  \ Helper.check<?>(tree,keyString,"a")
  \ Helper.check<?>(tree,keyValue,Value.new())

  \ tree.remove(keyString)

  \ Helper.check<?>(tree,keyString,empty)
}

unittest wrongReturnType {
  TypeMap tree <- TypeMap.new()

  TypeKey<String> keyString <- TypeKey<String>.new()

  // Added as Formatted, which means it cannot be retrieved as String later.
  \ tree.set<Formatted>(keyString,"a")
  \ Helper.check<String>(tree,keyString,empty)

  \ tree.set<String>(keyString,"a")
  \ Helper.check<String>(tree,keyString,"a")
}

concrete Value {
  refines Formatted
  defines Equals<Value>

  @type new () -> (Value)
}

define Value {
  new () {
    return Value{ }
  }

  formatted () {
    return "Value"
  }

  equals (_,_) {
    return true
  }
}

concrete Helper {
  @type check<#x>
    #x requires Formatted
    #x defines Equals<#x>
  (TypeMap,TypeKey<#x>,optional #x) -> ()
}

define Helper {
  check (tree,key,expected) {
    scoped {
      optional #x value <- tree.get<#x>(key)
    } in if (present(value) && present(expected)) {
      \ Testing.checkEquals<?>(require(value),require(expected))
    } elif (!present(value) && present(expected)) {
      fail(key.formatted() + " was expected but not found")
    } elif (present(value) && !present(expected)) {
      fail(key.formatted() + " was found but not expected")
    }
  }
}
