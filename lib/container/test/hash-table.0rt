/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "HashTable tests" {
  success
}

unittest integrationTest {
  HashTable<Int,Int> tree <- HashTable<Int,Int>.default()
  Int count <- 33
  $ReadOnly[count]$

  // Insert values.
  traverse (Counter.zeroIndexed(count) -> Int i) {
    Int new <- ((i + 13) * 3547) % count
    \ tree.set(new,i)
    \ Testing.checkEquals(tree.size(),i+1)
  }

  // Check and remove values.
  traverse (Counter.zeroIndexed(count) -> Int i) {
    Int new <- ((i + 13) * 3547) % count
    scoped {
      optional Int value <- tree.get(new)
    } in if (!present(value)) {
      \ BasicOutput.error()
          .write("Not found ")
          .write(new)
          .write(" but should have been ")
          .write(i)
          .flush()
    } elif (require(value) != i) {
      \ BasicOutput.error()
          .write("Element ")
          .write(new)
          .write(" should have been ")
          .write(i)
          .write(" but was ")
          .write(require(value))
          .flush()
    }
    \ tree.remove(new)
    \ Testing.checkEquals(tree.size(),count-i-1)
  }
}

unittest removeNotPresent {
  HashTable<Int,Int> tree <- HashTable<Int,Int>.new().set(1,1).set(2,2).set(4,4)
  \ tree.remove(3)
  \ Testing.checkEquals(tree.size(),3)
}

unittest defaultOrder {
  HashTable<Int,Int> tree <- HashTable<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the tree in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `tree.set` i
  }

  Vector<Bool> visited <- Vector:createSize<Bool>(max)

  // Validate traversal coverage.
  Int index <- 0
  traverse (tree.defaultOrder() -> KeyValue<Int,Int> entry) {
    \ Testing.checkFalse(visited.readAt(entry.getKey()))
    \ Testing.checkEquals((entry.getValue()*hash)%max,entry.getKey())
    \ visited.writeAt(entry.getKey(),true)
    index <- index+1
  }

  \ Testing.checkEquals(index,max)
}

unittest defaultOrderEmpty {
  traverse (HashTable<Int,Int>.new().defaultOrder() -> _) {
    fail("not empty")
  }
}

unittest duplicate {
  HashTable<Int,Int> tree <- HashTable<Int,Int>.default()
  Int max <- 31
  $ReadOnly[max]$

  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ tree.set(i,i)
  }

  HashTable<Int,Int> copy <- tree.duplicate()
  \ Testing.checkEquals(copy.size(),tree.size())
  traverse (Counter.zeroIndexed(max) -> Int i) {
    $Hidden[tree]$
    \ Testing.checkOptional(copy.get(i),i)
  }

  \ copy.set(2,5)
  \ copy.remove(7)
  $Hidden[copy]$

  \ Testing.checkEquals(tree.size(),max)
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ Testing.checkOptional(tree.get(i),i)
  }
}

unittest duplicateEmpty {
  HashTable<Int,Int> tree <- HashTable<Int,Int>.new()
  HashTable<Int,Int> copy <- tree.duplicate()
  \ tree.set(1,1)
  \ Testing.checkEquals(copy.size(),0)
}

unittest weakSetExists {
  HashTable<Int,Value> tree <- HashTable<Int,Value>.default()
      .set(1,Value.new(1))
      .set(2,Value.new(2))
      .set(3,Value.new(3))

  Value value0 <- Value.new(5)
  Value value1 <- tree.weakSet(1,value0)
  \ Testing.checkEquals(tree.size(),3)

  \ Testing.checkEquals(value0.get(),5)
  \ Testing.checkEquals(value1.get(),1)

  \ value0.set(10)
  \ Testing.checkEquals(value1.get(),1)
  \ Testing.checkOptional(tree.get(1),Value.new(1))
}

unittest weakSetMissing {
  HashTable<Int,Value> tree <- HashTable<Int,Value>.default()
      .set(1,Value.new(1))
      .set(2,Value.new(2))
      .set(3,Value.new(3))

  Value value0 <- Value.new(5)
  Value value1 <- tree.weakSet(7,value0)
  \ Testing.checkEquals(tree.size(),4)

  \ Testing.checkEquals(value0.get(),5)
  \ Testing.checkEquals(value1.get(),5)

  \ value0.set(10)
  \ Testing.checkEquals(value1.get(),10)
  \ Testing.checkOptional(tree.get(7),Value.new(10))
}

concrete Value {
  defines Equals<Value>
  refines Formatted

  @type new (Int) -> (Value)
  @value get () -> (Int)
  @value set (Int) -> ()
}

define Value {
  @value Int value

  new (value) { return Value{ value } }
  get () { return value }
  set (value2) { value <- value2 }

  formatted () {
    return value.formatted()
  }

  equals (x,y) {
    return x.get() == y.get()
  }
}
