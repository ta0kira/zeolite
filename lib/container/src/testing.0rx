/* -----------------------------------------------------------------------------
Copyright 2023 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define CheckSet {
  equals (expected) {
    return SetEquals<#k>.new(expected)
  }
}

define CheckMap {
  using (expected) {
    return MapAdapter<#k, #v>.new(expected)
  }

  equals (expected) {
    return MapEquals<#k, #v>.new(expected)
  }

  matches (expected) {
    return MapMatches<#k, #v>.new(expected)
  }
}

concrete MapAdapter<#k, #v> {
  #v requires TestCompare<#v>

  @type new ([SetReader<#k> & DefaultOrder<KeyValue<#k, #v>>]) -> ([SetReader<#k> & DefaultOrder<KeyValue<#k, ValueMatcher<#v>>>])
}

define MapAdapter {
  $ReadOnlyExcept[current]$

  refines SetReader<#k>
  refines DefaultOrder<KeyValue<#k, ValueMatcher<#v>>>
  refines Order<KeyValue<#k, ValueMatcher<#v>>>

  @value [SetReader<#k> & DefaultOrder<KeyValue<#k, #v>>] original
  @value optional Order<KeyValue<#k, #v>> current

  new (original) {
    return #self{ original, empty }
  }

  defaultOrder () {
    return #self{ original, original.defaultOrder() }
  }

  get () {
    scoped {
      KeyValue<#k, #v> keyValue <- `require` current&.get()
    } in return SimpleKeyValue:new(keyValue.getKey(), CheckValue:using(keyValue.getValue()))
  }

  next () {
    if (`present` (current <- current&.next())) {
      return self
    } else {
      return empty
    }
  }

  member (k) {
    return delegate -> `original.member`
  }
}

concrete SetEquals<#k> {
  #k requires Formatted

  @type new ([SetReader<#k> & DefaultOrder<#k>]) -> (ValueMatcher<[SetReader<#k> & DefaultOrder<#k>]>)
}

define SetEquals {
  $ReadOnlyExcept[]$

  refines ValueMatcher<[SetReader<#k> & DefaultOrder<#k>]>

  @value [SetReader<#k> & DefaultOrder<#k>] expected

  new (expected) {
    return delegate -> #self
  }

  check (actual, report) {
    traverse (expected.defaultOrder() -> #k currentExpected) {
      if (`present` actual && ! `require(actual).member` currentExpected) {
        \ report.addError(message: String.builder()
            .append("\"")
            .append(currentExpected)
            .append("\" is missing")
            .build())
      }
    }
    traverse (actual&.defaultOrder() -> #k currentActual) {
      if (! `expected.member` currentActual) {
        \ report.addError(message: String.builder()
            .append("\"")
            .append(currentActual)
            .append("\" is unexpected")
            .build())
      }
    }
  }

  summary () {
    return "set equals"
  }
}

concrete MapEquals<#k, #v> {
  #k requires Formatted
  #v requires Formatted
  #v defines Equals<#v>

  @type new ([SetReader<#k> & DefaultOrder<KeyValue<#k, #v>>]) -> (ValueMatcher<[KVReader<#k, #v> & KeyOrder<#k>]>)
}

define MapEquals {
  $ReadOnlyExcept[]$

  refines ValueMatcher<[KVReader<#k, #v> & KeyOrder<#k>]>

  @value [SetReader<#k> & DefaultOrder<KeyValue<#k, #v>>] expected

  new (expected) {
    return delegate -> #self
  }

  check (actual, report) {
    traverse (expected.defaultOrder() -> KeyValue<#k, #v> current) {
      optional #v value <- actual&.get(current.getKey())
      $Hidden[actual, expected]$
      if (! `present` value) {
        \ report.addError(message: String.builder()
            .append("Key ")
            .append(`Format:autoFormat` current.getKey())
            .append(" is missing")
            .build())
      } elif (!(require(value) `#v.equals` current.getValue())) {
        TestReport newReport <- report.newSection(title: String.builder()
            .append("Key ")
            .append(`Format:autoFormat` current.getKey())
            .build())
        $Hidden[report]$
        \ newReport.addError(message: String.builder()
            .append(`Format:autoFormat` value)
            .append(" does not equal ")
            .append(`Format:autoFormat` current.getValue())
            .build())
      }
    }
    traverse (actual&.keyOrder() -> #k current) {
      if (! `expected.member` current) {
        \ report.addError(message: String.builder()
            .append("Key ")
            .append(`Format:autoFormat` current)
            .append(" is unexpected")
            .build())
      }
    }
  }

  summary () {
    return "map equals"
  }
}

concrete MapMatches<#k, #v> {
  #k requires Formatted

  @type new ([SetReader<#k> & DefaultOrder<KeyValue<#k, ValueMatcher<#v>>>]) -> (ValueMatcher<[KVReader<#k, #v> & KeyOrder<#k>]>)
}

define MapMatches {
  $ReadOnlyExcept[]$

  refines ValueMatcher<[KVReader<#k, #v> & KeyOrder<#k>]>

  @value [SetReader<#k> & DefaultOrder<KeyValue<#k, ValueMatcher<#v>>>] expected

  new (expected) {
    return delegate -> #self
  }

  check (actual, report) {
    traverse (expected.defaultOrder() -> KeyValue<#k, ValueMatcher<#v>> current) {
      optional #v value <- actual&.get(current.getKey())
      $Hidden[actual, expected]$
      if (! `present` value) {
        \ report.addError(message: String.builder()
            .append("Key ")
            .append(`Format:autoFormat` current.getKey())
            .append(" is missing")
            .build())
      } else {
        TestReport newReport <- report.newSection(title: String.builder()
            .append("Key ")
            .append(`Format:autoFormat` current.getKey())
            .build())
        $Hidden[report]$
        \ require(value) `current.getValue().check` newReport
      }
    }
    traverse (actual&.keyOrder() -> #k current) {
      if (! `expected.member` current) {
        \ report.addError(message: String.builder()
            .append("Key ")
            .append(`Format:autoFormat` current)
            .append(" is unexpected")
            .build())
      }
    }
  }

  summary () {
    return "map matches"
  }
}
