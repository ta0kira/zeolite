/* -----------------------------------------------------------------------------
Copyright 2020-2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

$TestsOnly$

define Testing {
  checkEquals (x,y) { $NoTrace$
    if (!#x.equals(x,y)) {
      fail(String.builder()
          .append(x)
          .append(" is not equal to ")
          .append(y)
          .build())
    }
  }

  checkNotEquals (x,y) { $NoTrace$
    if (#x.equals(x,y)) {
      fail(String.builder()
          .append(x)
          .append(" is equal to ")
          .append(y)
          .build())
    }
  }

  checkEmpty (x) { $NoTrace$
    if (present(x)) {
      scoped {
        optional Formatted formatted <- reduce<#x,Formatted>(require(x))
      } in if (present(formatted)) {
        fail(String.builder()
            .append(require(formatted))
            .append(" is not empty")
            .build())
      } else {
        fail("(unformatted value) is not empty")
      }
    }
  }

  checkPresent (x) { $NoTrace$
    if (!present(x)) {
      fail(String.builder()
          .append("expected non-empty value")
          .build())
    }
  }

  checkOptional (x,y) { $NoTrace$
    if (present(x) && present(y)) {
      \ checkEquals(require(x),require(y))
    } elif (!present(x) && present(y)) {
      fail("empty is not equal to " + require(y).formatted())
    } elif (present(x) && !present(y)) {
      fail(require(x).formatted() + " is not equal to empty")
    }
  }

  checkTrue (x) { $NoTrace$
    if (!x.asBool()) {
      fail("expected true")
    }
  }

  checkFalse (x) { $NoTrace$
    if (x.asBool()) {
      fail("expected false")
    }
  }

  checkBetween (x,l,h) { $NoTrace$
    if (!lessEquals(l,h)) {
      fail(String.builder()
          .append("Upper limit ")
          .append(l)
          .append(" is not at or above lower limit ")
          .append(h)
          .build())
    }
    if (!lessEquals(l,x) || !lessEquals(x,h)) {
      fail(String.builder()
          .append(x)
          .append(" is not between ")
          .append(l)
          .append(" and ")
          .append(h)
          .build())
    }
  }

  checkGreaterThan (x,l) { $NoTrace$
    if (lessEquals(x,l)) {
      fail(String.builder()
          .append(x)
          .append(" is not greater than ")
          .append(l)
          .build())
    }
  }

  checkLessThan (x,h) { $NoTrace$
    if (lessEquals(h,x)) {
      fail(String.builder()
          .append(x)
          .append(" is not less than ")
          .append(h)
          .build())
    }
  }

  @type lessEquals<#x>
    #x defines Equals<#x>
    #x defines LessThan<#x>
  (#x,#x) -> (Bool)
  lessEquals (x,y) { $NoTrace$
    // Using !#x.lessThan(y,x) wouldn't account for NaNs.
    return #x.lessThan(x,y) || #x.equals(x,y)
  }
}
