/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "comparator helper tests" {
  success
}

unittest orderHLessThan {
  \ Testing.checkFalse("".defaultOrder()    `OrderH:lessThan` "".defaultOrder())
  \ Testing.checkTrue("a".defaultOrder()    `OrderH:lessThan` "abc".defaultOrder())
  \ Testing.checkFalse("b".defaultOrder()   `OrderH:lessThan` "abc".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:lessThan` "abc".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:lessThan` "a".defaultOrder())
  \ Testing.checkTrue("abc".defaultOrder()  `OrderH:lessThan` "b".defaultOrder())
}

unittest orderHLessThanWith {
  \ Testing.checkFalse("".defaultOrder()    `OrderH:lessThanWith<?,Reversed<Char>>` "".defaultOrder())
  \ Testing.checkTrue("a".defaultOrder()    `OrderH:lessThanWith<?,Reversed<Char>>` "abc".defaultOrder())
  \ Testing.checkTrue("b".defaultOrder()    `OrderH:lessThanWith<?,Reversed<Char>>` "abc".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:lessThanWith<?,Reversed<Char>>` "abc".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:lessThanWith<?,Reversed<Char>>` "a".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:lessThanWith<?,Reversed<Char>>` "b".defaultOrder())
}

unittest orderHEquals {
  \ Testing.checkTrue("".defaultOrder()     `OrderH:equals` "".defaultOrder())
  \ Testing.checkFalse("a".defaultOrder()   `OrderH:equals` "abc".defaultOrder())
  \ Testing.checkTrue("abc".defaultOrder()  `OrderH:equals` "abc".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:equals` "a".defaultOrder())
}

unittest orderHEqualsWith {
  \ Testing.checkTrue("".defaultOrder()     `OrderH:equalsWith<?,IgnoreCase>` "".defaultOrder())
  \ Testing.checkFalse("a".defaultOrder()   `OrderH:equalsWith<?,IgnoreCase>` "ABC".defaultOrder())
  \ Testing.checkTrue("abc".defaultOrder()  `OrderH:equalsWith<?,IgnoreCase>` "ABC".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:equalsWith<?,IgnoreCase>` "DEF".defaultOrder())
  \ Testing.checkFalse("abc".defaultOrder() `OrderH:equalsWith<?,IgnoreCase>` "A".defaultOrder())
}

unittest readAtHLessThan {
  \ Testing.checkFalse(""    `ReadAtH:lessThan` "")
  \ Testing.checkTrue("a"    `ReadAtH:lessThan` "abc")
  \ Testing.checkFalse("b"   `ReadAtH:lessThan` "abc")
  \ Testing.checkFalse("abc" `ReadAtH:lessThan` "abc")
  \ Testing.checkFalse("abc" `ReadAtH:lessThan` "a")
  \ Testing.checkTrue("abc"  `ReadAtH:lessThan` "b")
}

unittest readAtHLessThanWith {
  \ Testing.checkFalse(""    `ReadAtH:lessThanWith<?,Reversed<Char>>` "")
  \ Testing.checkTrue("a"    `ReadAtH:lessThanWith<?,Reversed<Char>>` "abc")
  \ Testing.checkTrue("b"    `ReadAtH:lessThanWith<?,Reversed<Char>>` "abc")
  \ Testing.checkFalse("abc" `ReadAtH:lessThanWith<?,Reversed<Char>>` "abc")
  \ Testing.checkFalse("abc" `ReadAtH:lessThanWith<?,Reversed<Char>>` "a")
  \ Testing.checkFalse("abc" `ReadAtH:lessThanWith<?,Reversed<Char>>` "b")
}

unittest readAtHEquals {
  \ Testing.checkTrue(""     `ReadAtH:equals` "")
  \ Testing.checkFalse("a"   `ReadAtH:equals` "abc")
  \ Testing.checkTrue("abc"  `ReadAtH:equals` "abc")
  \ Testing.checkFalse("abc" `ReadAtH:equals` "a")
}

unittest readAtHEqualsWith {
  \ Testing.checkTrue(""     `ReadAtH:equalsWith<?,IgnoreCase>` "")
  \ Testing.checkFalse("a"   `ReadAtH:equalsWith<?,IgnoreCase>` "ABC")
  \ Testing.checkTrue("abc"  `ReadAtH:equalsWith<?,IgnoreCase>` "ABC")
  \ Testing.checkFalse("abc" `ReadAtH:equalsWith<?,IgnoreCase>` "DEF")
  \ Testing.checkFalse("abc" `ReadAtH:equalsWith<?,IgnoreCase>` "a")
}

concrete IgnoreCase {
  defines Equals<Char>
}

define IgnoreCase {
  equals (x,y) {
    return toLower(x) == toLower(y)
  }

  @type toLower (Char) -> (Char)
  toLower (c) {
    if (c >= 'A' && c <= 'Z') {
      return (c.asInt() - 'A'.asInt() + 'a'.asInt()).asChar()
    } else {
      return c
    }
  }
}

unittest reversed {
  \ Testing.checkFalse(0 `Reversed<Int>.lessThan` 0)
  \ Testing.checkFalse(0 `Reversed<Int>.lessThan` 1)
  \ Testing.checkTrue(1  `Reversed<Int>.lessThan` 0)
}

unittest bySizeLessThan {
  \ Testing.checkFalse(""   `BySize.lessThan` "")
  \ Testing.checkTrue("b"   `BySize.lessThan` "ac")
  \ Testing.checkFalse("ac" `BySize.lessThan` "b")
  \ Testing.checkFalse("a"  `BySize.lessThan` "b")
}

unittest bySizeEquals {
  \ Testing.checkTrue(""    `BySize.equals` "")
  \ Testing.checkFalse("b"  `BySize.equals` "ac")
  \ Testing.checkFalse("ac" `BySize.equals` "b")
  \ Testing.checkTrue("a"   `BySize.equals` "b")
}

unittest alwaysEqualLessThan {
  \ Testing.checkFalse(""   `AlwaysEqual.lessThan` "")
  \ Testing.checkFalse("b"  `AlwaysEqual.lessThan` "ac")
  \ Testing.checkFalse("ac" `AlwaysEqual.lessThan` "b")
}

unittest alwaysEqualEquals {
  \ Testing.checkTrue(""   `AlwaysEqual.equals` "")
  \ Testing.checkTrue("b"  `AlwaysEqual.equals` "ac")
  \ Testing.checkTrue("ac" `AlwaysEqual.equals` "b")
  \ Testing.checkTrue("a"  `AlwaysEqual.equals` "b")
}


testcase "iteration tests" {
  success
}

unittest orderHCopyTo {
  ValueCheck output <- ValueCheck.new(10)
  \ ValueOrder.wrap(false,Counter.zeroIndexed(10)) `OrderH:copyTo` output
  \ output.checkEnd()
}

unittest orderHDuplicateTo {
  ValueCheck output <- ValueCheck.new(10)
  \ ValueOrder.wrap(true,Counter.zeroIndexed(10)) `OrderH:duplicateTo` output
  \ output.checkEnd()
}

unittest readAtHCopyTo {
  ValueCheck output <- ValueCheck.new(10)
  \ ValueSource.new(false,10) `ReadAtH:copyTo` output
  \ output.checkEnd()
}

unittest readAtHDuplicateTo {
  ValueCheck output <- ValueCheck.new(10)
  \ ValueSource.new(true,10) `ReadAtH:duplicateTo` output
  \ output.checkEnd()
}

unittest readAtHForwardOrder {
  ValueSource source <- ValueSource.new(false,10)
  Int expected <- 0
  traverse (`ReadAtH:forwardOrder` source -> Value actual) {
    \ Testing.checkEquals(actual.get(),expected)
  } update {
    expected <- expected+1
  }
  \ Testing.checkEquals(expected,10)
}

unittest readAtHReverseOrder {
  ValueSource source <- ValueSource.new(false,10)
  Int expected <- 9
  traverse (`ReadAtH:reverseOrder` source -> Value actual) {
    \ Testing.checkEquals(actual.get(),expected)
  } update {
    expected <- expected-1
  }
  \ Testing.checkEquals(expected,-1)
}

concrete ValueOrder {
  refines Order<Value>

  @type wrap (Bool,optional Order<Int>) -> (optional Order<Value>)
}

define ValueOrder {
  $ReadOnly[original]$

  @value Bool original
  @value Order<Int> order

  wrap (original,order) {
    if (!present(order)) {
      return empty
    } else {
      return ValueOrder{ original, require(order) }
    }
  }

  next () {
    optional Order<Int> order2 <- order.next()
    if (present(order2)) {
      order <- require(order2)
      return self
    } else {
      return empty
    }
  }

  get () {
    return Value.new(original,order.get())
  }
}

concrete ValueCheck {
  // Must append the next value in the sequence [0,size).
  refines Append<Value>

  @type new (Int) -> (ValueCheck)
  @value checkEnd () -> ()
}

define ValueCheck {
  $ReadOnly[target]$

  @value Int current
  @value Int target

  new (target) {
    return ValueCheck{ 0, target }
  }

  checkEnd () {
    \ Testing.checkEquals(current,target)
  }

  append (x) {
    \ Testing.checkEquals(x.get(),current)
    current <- current+1
    return self
  }
}

concrete Value {
  // Crashes if "original" is already false.
  refines Duplicate

  // Args:
  // - Bool: Whether or not the value is "original".
  // - Int: Value to return from get().
  @type new (Bool,Int) -> (Value)

  // Crashes if "original" is true.
  @value get () -> (Int)
}

define Value {
  $ReadOnly[original,value]$

  @value Bool original
  @value Int value

  new (original,value) {
    return Value{ original, value }
  }

  get () {
    if (original) {
      fail("not duplicated")
    }
    return value
  }

  duplicate () {
    if (!original) {
      fail("already duplicated")
    }
    return Value{ false, value }
  }
}

concrete ValueSource {
  // readAt(i) returns Value.new(original,i).
  refines ReadAt<Value>

  // Args:
  // - Bool: Whether or not to mark new values as "original".
  // - Int: Total size.
  @type new (Bool,Int) -> (ValueSource)
}

define ValueSource {
  $ReadOnly[original,size]$

  @value Bool original
  @value Int size

  new (original,size) {
    return ValueSource{ original, size }
  }

  size () {
    return size
  }

  readAt (i) {
    \ Testing.checkBetween(i,0,size-1)
    return Value.new(original,i)
  }
}
