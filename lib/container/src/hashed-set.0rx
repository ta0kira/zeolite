/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define HashedSet {
  @value HashedMap<#k,any> map

  default () {
    return new()
  }

  new () {
    return #self{ HashedMap<#k,any>.new() }
  }

  size () {
    return map.size()
  }

  duplicate () {
    return #self{ map.duplicate() }
  }

  add (k) {
    \ map.set(k,Void.default())
    return self
  }

  append (k) {
    return add(k)
  }

  remove (k) {
    \ map.remove(k)
    return self
  }

  member (k) {
    return present(map.get(k))
  }

  defaultOrder () {
    return HashedSetOrder<#k>.new(map)
  }
}

concrete HashedSetOrder<#k> {
  refines Order<#k>

  @type new (HashedMap<#k,any>) -> (optional #self)
}

define HashedSetOrder {
  @value Order<KeyValue<#k,any>> keyValue

  new (map) {
    scoped {
      optional Order<KeyValue<#k,any>> keyValue <- map.defaultOrder()
    } in if (`present` keyValue) {
      return #self{ `require` keyValue }
    } else {
      return empty
    }
  }

  get () {
    return keyValue.get().getKey()
  }

  next () {
    scoped {
      optional Order<KeyValue<#k,any>> keyValue2 <- keyValue.next()
    } in if (`present` keyValue2) {
      keyValue <- `require` keyValue2
      return self
    } else {
      return empty
    }
  }
}
