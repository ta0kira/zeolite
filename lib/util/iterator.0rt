/* -----------------------------------------------------------------------------
Copyright 2019-2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "iterate ReadIterator" {
  success
}

unittest forwardRead {
  String s <- "abcde"
  Int count <- 0
  scoped {
    ReadIterator<Char> iter <- ReadIterator:fromReadAt<?>(s)
  } in while (!iter.pastForwardEnd()) {
    if (iter.readCurrent() != s.readAt(count)) {
      fail(iter.readCurrent())
    }
  } update {
    count <- count+1
    iter <- iter.forward()
  }
  if (count != s.readSize()) {
    fail(count)
  }
}

unittest reverseRead {
  String s <- "abcde"
  Int count <- s.readSize()-1
  scoped {
    ReadIterator<Char> iter <- ReadIterator:fromReadAtOffset<?>(s,count)
  } in while (!iter.pastReverseEnd()) {
    if (iter.readCurrent() != s.readAt(count)) {
      fail(iter.readCurrent())
    }
  } update {
    count <- count-1
    iter <- iter.reverse()
  }
  if (count != -1) {
    fail(count)
  }
}


testcase "iterate ReadIterator" {
  success
}

unittest forwardWrite {
  String s <- "abcde"
  Int count <- 0
  scoped {
    WriteChecker checker <- WriteChecker.create(s)
    WriteIterator<Char> iter <- WriteIterator:fromWriteAt<?>(checker)
  } in while (!iter.pastForwardEnd()) {
    \ iter.writeCurrent(s.readAt(count))
  } update {
    count <- count+1
    iter <- iter.forward()
  }
  if (count != s.readSize()) {
    fail(count)
  }
}

unittest reverseWrite {
  String s <- "abcde"
  Int count <- s.readSize()-1
  scoped {
    WriteChecker checker <- WriteChecker.create(s)
    WriteIterator<Char> iter <- WriteIterator:fromWriteAtOffset<?>(checker,count)
  } in while (!iter.pastReverseEnd()) {
    \ iter.writeCurrent(s.readAt(count))
  } update {
    count <- count-1
    iter <- iter.reverse()
  }
  if (count != -1) {
    fail(count)
  }
}

concrete WriteChecker {
  refines WriteAt<Char>

  @type create (String) -> (WriteChecker)
}

define WriteChecker {
  @value String content

  create (string) {
    return WriteChecker{ string }
  }

  writeAt (c,index) {
    \ Testing.checkEquals<?>(c,content.readAt(index))
    return self
  }

  writeSize () {
    return content.readSize()
  }
}
