/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

concrete SimulateRefs {
  @type run () -> ()
}

define SimulateRefs {
  @category Int errorLimit <- 10
  @category RandomUniform random <- RandomUniform.probability().setSeed(Realtime.monoSeconds().asInt())

  run () {
    Bool useBroken <- Argv.global().size() > 1 && Argv.global().readAt(1) == "broken"

    if (Argv.global().size() > 1 && !useBroken) {
      fail("Pass no args or \"broken\"")
    }

    traverse (Counter.unlimited() -> Int i) {
      \ BasicOutput.stderr()
          .write("Iteration ")
          .write(i)
          .write("\n")
          .flush()

      if (!runOnce(useBroken) && (errorLimit <- errorLimit-1) == 0) {
        break
      }
    }
  }

  @type getRoutines (ReferenceState) -> (DefaultOrder<StateMachine>)
  getRoutines (state) {
    return Vector<StateMachine>.new()
        .append(Routines.newShared("shared1",state))
        .append(Routines.newWeak("weak1",state))
        .append(Routines.newWeak("weak2",state))
  }

  @type getRoutinesBroken (ReferenceState) -> (DefaultOrder<StateMachine>)
  getRoutinesBroken (state) {
    return Vector<StateMachine>.new()
        .append(Routines.newSharedBroken("shared1",state))
        .append(Routines.newWeakBroken("weak1",state))
        .append(Routines.newWeakBroken("weak2",state))
  }

  @type runOnce (Bool) -> (Bool)
  runOnce (useBroken) {
    ReferenceState state <- ReferenceState.new()
    DefaultOrder<StateMachine> routines <- defer
    if (useBroken) {
      routines <- getRoutinesBroken(state)
    } else {
      routines <- getRoutines(state)
    }
    \ StateExecutor:multiplexStates(routines,random)

    scoped {
      optional String error <- state.getError()
      if (!present(error) && (state.hasStrong() || state.hasWeak())) {
        error <- "expected zero final references"
      }
      if (!present(error) && state.hasLock()) {
        error <- "expected unlocked final reference"
      }
    } in if (present(error)) {
      \ BasicOutput.stdout()
          .write("Error in final state: ")
          .write(require(error))
          .write("\nFinal state: ")
          .write(state)
          .write("\n")
          .flush()
      traverse (state.getOperations() -> Formatted operation) {
        \ BasicOutput.stdout()
            .write("  ")
            .write(operation)
            .write("\n")
            .flush()
      }
      return false
    } else {
      return true
    }
  }
}
