/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define HashSet {
  @value HashTable<#k,any> tree

  default () {
    return new()
  }

  new () {
    return #self{ HashTable<#k,any>.new() }
  }

  size () {
    return tree.size()
  }

  duplicate () {
    return #self{ tree.duplicate() }
  }

  add (k) {
    \ tree.set(k,Void.void())
    return self
  }

  append (k) {
    return add(k)
  }

  remove (k) {
    \ tree.remove(k)
    return self
  }

  member (k) {
    return present(tree.get(k))
  }

  defaultOrder () {
    return HashSetOrder<#k>.new(tree)
  }
}

concrete HashSetOrder<#k> {
  refines Order<#k>

  @type new (HashTable<#k,any>) -> (optional #self)
}

define HashSetOrder {
  @value Order<KeyValue<#k,any>> keyValue

  new (tree) {
    scoped {
      optional Order<KeyValue<#k,any>> keyValue <- tree.defaultOrder()
    } in if (`present` keyValue) {
      return #self{ `require` keyValue }
    } else {
      return empty
    }
  }

  get () {
    return keyValue.get().getKey()
  }

  next () {
    scoped {
      optional Order<KeyValue<#k,any>> keyValue2 <- keyValue.next()
    } in if (`present` keyValue2) {
      keyValue <- `require` keyValue2
      return self
    } else {
      return empty
    }
  }
}
