/* -----------------------------------------------------------------------------
Copyright 2021,2024 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define OrderH {
  lessThan (x, y) {
    return lessThanWith<#x, #x>(x, y)
  }

  lessThanWith (x, y) {
    optional Order<#x> xx <- x
    optional Order<#x> yy <- y
    $Hidden[x, y]$
    while (present(xx) && present(yy)) {
      $ReadOnly[xx, yy]$
      if (require(xx).get() `#xx.lessThan` require(yy).get()) {
        return true
      } elif (require(yy).get() `#xx.lessThan` require(xx).get()) {
        return false
      }
    } update {
      xx <- require(xx).next()
      yy <- require(yy).next()
    }
    return present(yy)
  }

  equals (x, y) {
    return equalsWith<#x, #x>(x, y)
  }

  equalsWith (x, y) {
    optional Order<#x> xx <- x
    optional Order<#x> yy <- y
    $Hidden[x, y]$
    while (present(xx) && present(yy)) {
      $ReadOnly[xx, yy]$
      if (!(require(xx).get() `#xx.equals` require(yy).get())) {
        return false
      }
    } update {
      xx <- require(xx).next()
      yy <- require(yy).next()
    }
    return !present(xx) && !present(yy)
  }

  copyTo (input, output) (original) {
    original <- output
    $Hidden[original]$
    traverse (input -> #x value) {
      \ output.append(value)
    }
  }

  duplicateTo (input, output) (original) {
    original <- output
    $Hidden[original]$
    traverse (input -> #x value) {
      \ output.append(value.duplicate())
    }
  }
}

define ReadAtH {
  lessThan (x, y) {
    return lessThanWith<#x, #x>(x, y)
  }

  lessThanWith (x, y) {
    traverse (`Counter.zeroIndexed` (x.size() `Ranges:min` y.size()) -> Int i) {
      $ReadOnly[i]$
      if (x.readAt(i) `#xx.lessThan` y.readAt(i)) {
        return true
      } elif (y.readAt(i) `#xx.lessThan` x.readAt(i)) {
        return false
      }
    }
    return x.size() < y.size()
  }

  equals (x, y) {
    return equalsWith<#x, #x>(x, y)
  }

  equalsWith (x, y) {
    if (x.size() != y.size()) {
      return false
    }
    traverse (`Counter.zeroIndexed` x.size() -> Int i) {
      $ReadOnly[i]$
      if (!(x.readAt(i) `#xx.equals` y.readAt(i))) {
        return false
      }
    }
    return true
  }

  copyTo (input, output) (original) {
    original <- output
    $Hidden[original]$
    traverse (`Counter.zeroIndexed` input.size() -> Int i) {
      \ output.append(input.readAt(i))
    }
  }

  duplicateTo (input, output) (original) {
    original <- output
    $Hidden[original]$
    traverse (`Counter.zeroIndexed` input.size() -> Int i) {
      \ output.append(input.readAt(i).duplicate())
    }
  }

  forwardOrder (input) {
    return input `iterateWith` `Counter.zeroIndexed` input.size()
  }

  reverseOrder (input) {
    return input `iterateWith` `Counter.revZeroIndexed` input.size()
  }

  iterateWith (input, order) {
    return input `IndexOrder:iterateWith` order
  }
}

define FormattedH {
  try (x) {
    if (present(x)) {
      scoped {
        optional Formatted formatted <- reduce<#x, Formatted>(x)
      } in if (present(formatted)) {
        return require(formatted)
      } else {
        return String.builder()
            .append(typename<#x>())
            .append("{?}")
            .build()
      }
    } else {
        return String.builder()
            .append(typename<#x>())
            .append("{empty}")
            .build()
    }
  }
}

concrete IndexOrder<#x> {
  refines Order<#x>

  @category iterateWith<#x> (ReadAt<#x>, optional Order<Int>) -> (optional Order<#x>)
}

define IndexOrder {
  $ReadOnly[container]$

  @value ReadAt<#x> container
  @value Order<Int> index

  iterateWith (container, index) {
    if (`present` index) {
      return IndexOrder<#x>{ container, `require` index }
    } else {
      return empty
    }
  }

  get () {
    return container.readAt(index.get())
  }

  next () {
    scoped {
      optional Order<Int> index2 <- index.next()
    } in if (`present` index2) {
      index <- `require` index2
      return self
    } else {
      return empty
    }
  }
}

define Reversed {
  lessThan (x, y) {
    return y `#x.lessThan` x
  }
}

define BySize {
  equals (x, y) {
    return x.size() == y.size()
  }

  lessThan (x, y) {
    return x.size() < y.size()
  }
}

define AlwaysEqual {
  equals (_, _) {
    return true
  }

  lessThan (_, _) {
    return false
  }
}

define AsLessThan2 {
  refines LessThan2<#x>

  new () {
    return delegate -> #self
  }

  lessThan2 (x, y) {
    return x `#xx.lessThan` y
  }
}

define AppendH {
  from (output, input) {
    traverse (input -> #x value) {
      \ output.append(value)
    }
    return output
  }
}
