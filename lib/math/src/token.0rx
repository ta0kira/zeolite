/* -----------------------------------------------------------------------------
Copyright 2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define Token {
  $ReadOnly[mutex,toToken]$

  @category Mutex mutex <- SpinlockMutex.new()
  @category HashedMap<String,Token> toToken <- HashedMap<String,Token>.new()
  @category Int counter <- 0

  @value String string
  @value Int    value

  from (string) (token) {
    scoped {
      \ mutex.lock()
      optional Token existing <- toToken.get(string)
    } cleanup {
      \ mutex.unlock()
    } in if (present(existing)) {
      return require(existing)
    } else {
      \ toToken.set(string,(token <- Token{ string, (counter <- counter+1) }))
    }
  }

  formatted () {
    return string
  }

  hashed () {
    return value.hashed()
  }

  equals (x,y) {
    return x.get() == y.get()
  }

  lessThan (x,y) {
    return x.get() < y.get()
  }

  @value get () -> (Int)
  get () { return value }
}
