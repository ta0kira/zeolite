/* -----------------------------------------------------------------------------
Copyright 2019-2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define TextReader {
  @value BlockReader<String> reader
  @value String buffer

  fromBlockReader (reader) {
    return TextReader{ reader, "" }
  }

  readAll (reader) {
    [Append<Formatted> & Build<String>] builder <- String.builder()
    while (!reader.pastEnd()) {
      \ builder.append(reader.readBlock($ExprLookup[BLOCK_READ_SIZE]$))
    }
    return builder.build()
  }

  readNextLine () (line) {
    line <- ""
    while (true) {
      Int newline <- findNewline()
      if (newline < 0) {
        if (reader.pastEnd()) {
          line <- buffer
          buffer <- ""
          break
        } else {
          buffer <- buffer+reader.readBlock($ExprLookup[BLOCK_READ_SIZE]$)
        }
      } else {
        return takeLine(newline)
      }
    }
  }

  pastEnd () {
    return buffer.size() == 0 && reader.pastEnd()
  }

  @value findNewline () -> (Int)
  findNewline () {
    traverse (Counter.zeroIndexed(buffer.size()) -> Int position) {
      $ReadOnly[position]$
      Char c <- buffer.readAt(position)
      // TODO: Maybe the line separator should be a factory argument.
      if (c == '\n' || c == '\r') {
        return position
      }
    }
    return -1
  }

  @value takeLine (Int) -> (String)
  takeLine (position) {
    String data <- buffer.subSequence(0, position)
    buffer <- buffer.subSequence(position+1, buffer.size()-position-1)
    return data
  }
}
