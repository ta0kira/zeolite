/* -----------------------------------------------------------------------------
Copyright 2021,2023 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "HashedMap tests" {
  success
}

unittest integrationTest {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.default()
  Int count <- 33
  $ReadOnly[count]$

  // Insert values.
  traverse (Counter.zeroIndexed(count) -> Int i) {
    Int new <- ((i + 13) * 3547) % count
    \ map.set(new,i)
    \ Testing.checkEquals(map.size(),i+1)
  }

  // Check and remove values.
  traverse (Counter.zeroIndexed(count) -> Int i) {
    Int new <- ((i + 13) * 3547) % count
    scoped {
      optional Int value <- map.get(new)
    } in if (!present(value)) {
      \ BasicOutput.error()
          .write("Not found ")
          .write(new)
          .write(" but should have been ")
          .write(i)
          .flush()
    } elif (require(value) != i) {
      \ BasicOutput.error()
          .write("Element ")
          .write(new)
          .write(" should have been ")
          .write(i)
          .write(" but was ")
          .write(require(value))
          .flush()
    }
    \ map.remove(new)
    \ Testing.checkEquals(map.size(),count-i-1)
  }
}

unittest removeNotPresent {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.new().set(1,1).set(2,2).set(4,4)
  \ map.remove(3)
  \ Testing.checkEquals(map.size(),3)
}

unittest defaultOrder {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the map in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `map.set` i
  }

  Vector<Bool> visited <- Vector:createSize<Bool>(max)

  // Validate traversal coverage.
  Int index <- 0
  traverse (map.defaultOrder() -> KeyValue<Int,Int> entry) {
    \ Testing.checkFalse(visited.readAt(entry.getKey()))
    \ Testing.checkEquals((entry.getValue()*hash)%max,entry.getKey())
    \ visited.writeAt(entry.getKey(),true)
    index <- index+1
  }

  \ Testing.checkEquals(index,max)
}

unittest defaultOrderEmpty {
  traverse (HashedMap<Int,Int>.new().defaultOrder() -> _) {
    fail("not empty")
  }
}

unittest keyOrder {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the map in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `map.set` i
  }

  Vector<Bool> visited <- Vector:createSize<Bool>(max)

  // Validate traversal coverage.
  Int index <- 0
  traverse (map.keyOrder() -> Int key) {
    \ Testing.checkFalse(visited.readAt(key))
    \ visited.writeAt(key,true)
    index <- index+1
  }

  \ Testing.checkEquals(index,max)
}

unittest valueOrder {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the map in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `map.set` i
  }

  Vector<Bool> visited <- Vector:createSize<Bool>(max)

  // Validate traversal coverage.
  Int index <- 0
  traverse (map.valueOrder() -> Int value) {
    \ Testing.checkFalse(visited.readAt(value))
    \ visited.writeAt(value,true)
    index <- index+1
  }

  \ Testing.checkEquals(index,max)
}

unittest duplicate {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.default()
  Int max <- 31
  $ReadOnly[max]$

  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ map.set(i,i)
  }

  HashedMap<Int,Int> copy <- map.duplicate()
  \ Testing.checkEquals(copy.size(),map.size())
  traverse (Counter.zeroIndexed(max) -> Int i) {
    $Hidden[map]$
    \ Testing.checkEquals(copy.get(i),i)
  }

  \ copy.set(2,5)
  \ copy.remove(7)
  $Hidden[copy]$

  \ Testing.checkEquals(map.size(),max)
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ Testing.checkEquals(map.get(i),i)
  }
}

unittest duplicateEmpty {
  HashedMap<Int,Int> map <- HashedMap<Int,Int>.new()
  HashedMap<Int,Int> copy <- map.duplicate()
  \ map.set(1,1)
  \ Testing.checkEquals(copy.size(),0)
}

unittest weakSetExists {
  HashedMap<Int,Value> map <- HashedMap<Int,Value>.default()
      .set(1,Value.new(1))
      .set(2,Value.new(2))
      .set(3,Value.new(3))

  Value value0 <- Value.new(5)
  Value value1 <- map.weakSet(1,value0)
  \ Testing.checkEquals(map.size(),3)

  \ Testing.checkEquals(value0.get(),5)
  \ Testing.checkEquals(value1.get(),1)

  \ value0.set(10)
  \ Testing.checkEquals(value1.get(),1)
  \ Testing.checkEquals(map.get(1),Value.new(1))
}

unittest weakSetMissing {
  HashedMap<Int,Value> map <- HashedMap<Int,Value>.default()
      .set(1,Value.new(1))
      .set(2,Value.new(2))
      .set(3,Value.new(3))

  Value value0 <- Value.new(5)
  Value value1 <- map.weakSet(7,value0)
  \ Testing.checkEquals(map.size(),4)

  \ Testing.checkEquals(value0.get(),5)
  \ Testing.checkEquals(value1.get(),5)

  \ value0.set(10)
  \ Testing.checkEquals(value1.get(),10)
  \ Testing.checkEquals(map.get(7),Value.new(10))
}

concrete Value {
  defines Equals<Value>
  refines Formatted

  @type new (Int) -> (Value)
  @value get () -> (Int)
  @value set (Int) -> ()
}

define Value {
  @value Int value

  new (value) { return Value{ value } }
  get () { return value }
  set (value2) { value <- value2 }

  formatted () {
    return value.formatted()
  }

  equals (x,y) {
    return x.get() == y.get()
  }
}
