/* -----------------------------------------------------------------------------
Copyright 2019-2021 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "SearchTree tests" {
  success
}

unittest integrationTest {
  ValidatedTree<Int,Int> tree <- ValidatedTree<Int,Int>.new()
  Int count <- 33
  $ReadOnly[count]$

  // Insert values.
  traverse (Counter.zeroIndexed(count) -> Int i) {
    Int new <- ((i + 13) * 3547) % count
    \ tree.set(new,i)
    \ Testing.checkEquals(tree.size(),i+1)
  }

  // Check and remove values.
  traverse (Counter.zeroIndexed(count) -> Int i) {
    Int new <- ((i + 13) * 3547) % count
    scoped {
      optional Int value <- tree.get(new)
    } in if (!present(value)) {
      \ BasicOutput.error()
          .write("Not found ")
          .write(new)
          .write(" but should have been ")
          .write(i)
          .flush()
    } elif (require(value) != i) {
      \ BasicOutput.error()
          .write("Element ")
          .write(new)
          .write(" should have been ")
          .write(i)
          .write(" but was ")
          .write(require(value))
          .flush()
    }
    \ tree.remove(new)
    \ Testing.checkEquals(tree.size(),count-i-1)
  }
}

unittest defaultOrder {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the tree in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `tree.set` i
  }

  // Validate the traversal order.
  Int index <- 0
  traverse (tree.defaultOrder() -> KeyValue<Int,Int> entry) {
    \ Testing.checkEquals(entry.getKey(),index)
    \ Testing.checkEquals((entry.getValue()*hash)%max,entry.getKey())
    index <- index+1
  }

  \ Testing.checkEquals(index,max)
}

unittest reverseOrder {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the tree in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `tree.set` i
  }

  // Validate the traversal order.
  Int index <- max
  traverse (tree.reverseOrder() -> KeyValue<Int,Int> entry) {
    index <- index-1
    \ Testing.checkEquals(entry.getKey(),index)
    \ Testing.checkEquals((entry.getValue()*hash)%max,entry.getKey())
  }

  \ Testing.checkEquals(index,0)
}

unittest getForward {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the tree in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `tree.set` i
  }

  // Validate the traversal order from every starting point.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    Int index <- i
    traverse (tree.getForward(index) -> KeyValue<Int,Int> entry) {
      \ Testing.checkEquals(entry.getKey(),index)
      \ Testing.checkEquals((entry.getValue()*hash)%max,entry.getKey())
      index <- index+1
    }
    \ Testing.checkEquals(index,max)
  }
}

unittest getForwardNotFound {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.new()
  \ tree.set(1,1).set(3,3).set(5,5)

  scoped {
    optional Order<KeyValue<Int,Int>> start <- tree.getForward(4)
  } in if (!present(start)) {
    fail("Failed")
  } else {
    \ Testing.checkEquals(require(start).get().getKey(),5)
  }

  scoped {
    optional Order<KeyValue<Int,Int>> start <- tree.getForward(6)
  } in if (present(start)) {
    fail("Failed")
  }
}

unittest getReverse {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.new()
  Int hash <- 13
  Int max  <- 20
  $ReadOnly[hash,max]$

  // Populate the tree in a pseudo-random order.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    \ ((i*hash)%max) `tree.set` i
  }

  // Validate the traversal order from every starting point.
  traverse (Counter.zeroIndexed(max) -> Int i) {
    Int index <- i
    traverse (tree.getReverse(index) -> KeyValue<Int,Int> entry) {
      \ Testing.checkEquals(entry.getKey(),index)
      \ Testing.checkEquals((entry.getValue()*hash)%max,entry.getKey())
      index <- index-1
    }
    \ Testing.checkEquals(index,-1)
  }
}

unittest getReverseNotFound {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.new()
  \ tree.set(1,1).set(3,3).set(5,5)

  scoped {
    optional Order<KeyValue<Int,Int>> start <- tree.getReverse(2)
  } in if (!present(start)) {
    fail("Failed")
  } else {
    \ Testing.checkEquals(require(start).get().getKey(),1)
  }

  scoped {
    optional Order<KeyValue<Int,Int>> start <- tree.getReverse(0)
  } in if (present(start)) {
    fail("Failed")
  }
}

unittest duplicate {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.default().set(1,1).set(2,2).set(3,3)

  SearchTree<Int,Int> copy <- tree.duplicate()
  \ Testing.checkEquals(copy.size(),3)
  \ Testing.checkOptional(copy.get(1),1)
  \ Testing.checkOptional(copy.get(2),2)
  \ Testing.checkOptional(copy.get(3),3)

  \ copy.set(2,5)
  \ copy.remove(2)
  $Hidden[copy]$

  \ Testing.checkEquals(tree.size(),3)
  \ Testing.checkOptional(tree.get(1),1)
  \ Testing.checkOptional(tree.get(2),2)
  \ Testing.checkOptional(tree.get(3),3)
}

unittest duplicateEmpty {
  SearchTree<Int,Int> tree <- SearchTree<Int,Int>.default()

  SearchTree<Int,Int> copy <- tree.duplicate()
  \ Testing.checkEquals(copy.size(),0)
}

unittest weakSetExists {
  SearchTree<Int,Value> tree <- SearchTree<Int,Value>.default()
      .set(1,Value.new(1))
      .set(2,Value.new(2))
      .set(3,Value.new(3))

  Value value0 <- Value.new(5)
  Value value1 <- tree.weakSet(1,value0)
  \ Testing.checkEquals(tree.size(),3)

  \ Testing.checkEquals(value0.get(),5)
  \ Testing.checkEquals(value1.get(),1)

  \ value0.set(10)
  \ Testing.checkEquals(value1.get(),1)
  \ Testing.checkOptional(tree.get(1),Value.new(1))
}

unittest weakSetMissing {
  SearchTree<Int,Value> tree <- SearchTree<Int,Value>.default()
      .set(1,Value.new(1))
      .set(2,Value.new(2))
      .set(3,Value.new(3))

  Value value0 <- Value.new(5)
  Value value1 <- tree.weakSet(7,value0)
  \ Testing.checkEquals(tree.size(),4)

  \ Testing.checkEquals(value0.get(),5)
  \ Testing.checkEquals(value1.get(),5)

  \ value0.set(10)
  \ Testing.checkEquals(value1.get(),10)
  \ Testing.checkOptional(tree.get(7),Value.new(10))
}

concrete Value {
  defines Equals<Value>
  refines Formatted

  @type new (Int) -> (Value)
  @value get () -> (Int)
  @value set (Int) -> ()
}

define Value {
  @value Int value

  new (value) { return Value{ value } }
  get () { return value }
  set (value2) { value <- value2 }

  formatted () {
    return value.formatted()
  }

  equals (x,y) {
    return x.get() == y.get()
  }
}
