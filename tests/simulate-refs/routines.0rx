/* -----------------------------------------------------------------------------
Copyright 2021-2023 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

define Routines {
  @category optional StateTransition<String,ReferenceState> dropWeak <-
      Run:ifElse(
      /*if*/   DecrWeakIsZero.new(),
      /*then*/ FreeRef.new(),
      /*else*/ empty)

  @category optional StateTransition<String,ReferenceState> sharedThenDrop <-
      LockRef.new() `Run:then`
      Run:ifElse(
      /*if*/   IncrRefIsOne.new(),
      /*then*/ DecrRef.new() `Run:then`
               UnlockRef.new() `Run:then`
               dropWeak,
      /*else*/ UnlockRef.new() `Run:then`
               dropWeak `Run:then`
               dropShared)

  @category optional StateTransition<String,ReferenceState> dropShared <-
      LockRef.new() `Run:then`
      Run:ifElse(
      /*if*/   DecrRefIsZero.new(),
      /*then*/ FreeObject.new() `Run:then`
               UnlockRef.new() `Run:then`
               dropWeak,
      /*else*/ UnlockRef.new())

  @category optional StateTransition<String,ReferenceState> sharedThenDropBroken <-
      Run:ifElse(
      /*if*/   PlusLockModLockZero.new(),
      /*then*/ MinusLock.new() `Run:then`
               dropWeak,
      /*else*/ MinusLockPlusOne.new() `Run:then`
               dropWeak `Run:then`
               dropSharedBroken)

  @category optional StateTransition<String,ReferenceState> dropSharedBroken <-
      Run:ifElse(
      /*if*/   DecrRefIsZero.new(),
      /*then*/ FreeObject.new() `Run:then`
               dropWeak,
      /*else*/ empty)

  newShared (name,state) {
    if (state.addStrong() == 1) {
      \ state.addWeak()
    }
    return LabeledStateMachine:new(name,state,dropShared)
  }

  newWeak (name,state) {
    \ state.addWeak()
    return LabeledStateMachine:new(name,state,sharedThenDrop)
  }

  newSharedBroken (name,state) {
    if (state.addStrong() == 1) {
      \ state.addWeak()
    }
    return LabeledStateMachine:new(name,state,dropSharedBroken)
  }

  newWeakBroken (name,state) {
    \ state.addWeak()
    return LabeledStateMachine:new(name,state,sharedThenDropBroken)
  }
}
