/* -----------------------------------------------------------------------------
Copyright 2020 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "stdout writer" {
  success
  require stdout "message"
  exclude stderr "message"
}

unittest test {
  \ LazyStream<Formatted>.new().append("message").writeTo(SimpleOutput.stdout())
}


testcase "stderr writer" {
  success
  require stderr "message"
  exclude stdout "message"
}

unittest test {
  \ LazyStream<Formatted>.new().append("message").writeTo(SimpleOutput.stderr())
}


testcase "error writer" {
  crash
  require "message"
}

unittest test {
  \ LazyStream<Formatted>.new().append("message").writeTo(SimpleOutput.error())
}


testcase "iterate ReadIterator" {
  success
}

unittest forward {
  String s <- "abcde"
  Int count <- 0
  scoped {
    ReadIterator<Char> iter <- ReadIterator:fromReadPosition<Char>(s)
  } in while (!iter.pastForwardEnd()) {
    if (iter.readCurrent() != s.readPosition(count)) {
      fail(iter.readCurrent())
    }
  } update {
    count <- count+1
    iter <- iter.forward()
  }
  if (count != s.readSize()) {
    fail(count)
  }
}

unittest reverse {
  String s <- "abcde"
  Int count <- s.readSize()-1
  scoped {
    ReadIterator<Char> iter <- ReadIterator:fromReadPositionAt<Char>(s,count)
  } in while (!iter.pastReverseEnd()) {
    if (iter.readCurrent() != s.readPosition(count)) {
      fail(iter.readCurrent())
    }
  } update {
    count <- count-1
    iter <- iter.reverse()
  }
  if (count != -1) {
    fail(count)
  }
}


testcase "Argv is set and available" {
  success
  args "arg1" "arg2"
}

unittest test {
  Int count <- Argv.global().readSize()
  if (count != 3) {
    fail(count)
  }
  \ Testing.check<?>(Argv.global().readPosition(0),"testcase")
  \ Testing.check<?>(Argv.global().readPosition(1),"arg1")
  \ Testing.check<?>(Argv.global().readPosition(2),"arg2")
}


testcase "Argv subSequence" {
  success
  args "arg1" "arg2" "arg3"
}

unittest test {
  ReadPosition<String> argv <- Argv.global().subSequence(1,2)
  Int count <- argv.readSize()
  if (count != 2) {
    fail(count)
  }
  \ Testing.check<?>(argv.readPosition(0),"arg1")
  \ Testing.check<?>(argv.readPosition(1),"arg2")
}


testcase "Argv subSequence out of bounds" {
  crash
  require "position 5"
}

unittest test {
  ReadPosition<String> argv <- Argv.global().subSequence(5,1)
}


testcase "TextReader splits lines correctly" {
  success
}

unittest test {
  \ Test.run()
}

concrete FakeFile {
  refines BlockReader<String>

  @type create () -> (FakeFile)
}

define FakeFile {
  @value Int counter

  create () {
    return FakeFile{ 0 }
  }

  readBlock (_) {
    cleanup {
      counter <- counter+1
    } in if (counter == 0) {
      return "this is a "
    } elif (counter == 1) {
      return "partial line\nwith "
    } elif (counter == 2) {
      return "some more data\nand\nmore lines"
    } elif (counter == 3) {
      return "\nunfinished"
    } else {
      fail("too many reads")
    }
  }

  pastEnd () {
    return counter > 3
  }
}

define Test {
  run () {
    TextReader reader <- TextReader.fromBlockReader(FakeFile.create())
    \ checkNext(reader,"this is a partial line")
    \ checkNext(reader,"with some more data")
    \ checkNext(reader,"and")
    \ checkNext(reader,"more lines")
    \ checkNext(reader,"unfinished")
    if (!reader.pastEnd()) {
      fail("not past end")
    }
    if (reader.readNextLine() != "") {
      fail("reused data")
    }
  }

  @type checkNext (TextReader,String) -> ()
  checkNext (reader,line) {
    if (reader.pastEnd()) {
      fail("past end")
    }
    String value <- reader.readNextLine()
    if (value != line) {
      fail(value)
    }
  }
}

concrete Test {
  @type run () -> ()
}


testcase "TextReader readAll" {
  success
}

unittest test {
  String allContents <- TextReader.readAll(FakeFile.create())
  \ Testing.check<?>(allContents,"this is a partial line\nwith some more data\nand\nmore lines\nunfinished")
}

concrete FakeFile {
  refines BlockReader<String>

  @type create () -> (FakeFile)
}

define FakeFile {
  @value Int counter

  create () {
    return FakeFile{ 0 }
  }

  readBlock (_) {
    cleanup {
      counter <- counter+1
    } in if (counter == 0) {
      return "this is a "
    } elif (counter == 1) {
      return "partial line\nwith "
    } elif (counter == 2) {
      return "some more data\nand\nmore lines"
    } elif (counter == 3) {
      return "\nunfinished"
    } else {
      fail("too many reads")
    }
  }

  pastEnd () {
    return counter > 3
  }
}


testcase "ErrorOr value" {
  success
}

unittest test {
  ErrorOr<Int> value <- ErrorOr:value<Int>(10)
  if (value.isError()) {
    fail("Failed")
  }
  \ Testing.check<?>(value.getValue(),10)
}


testcase "ErrorOr getError() crashes with value" {
  crash
  require "empty"
}

unittest test {
  ErrorOr<Int> value <- ErrorOr:value<Int>(10)
  \ value.getError()
}


testcase "ErrorOr error" {
  success
}

unittest test {
  ErrorOr<String> value <- ErrorOr:error("error message")
  if (!value.isError()) {
    fail("Failed")
  }
  \ Testing.check<?>(value.getError().formatted(),"error message")
}


testcase "ErrorOr getValue() crashes with error" {
  crash
  require "error message"
}

unittest test {
  ErrorOr<String> value <- ErrorOr:error("error message")
  \ value.getValue()
}


testcase "ErrorOr convert error" {
  success
}

unittest test {
  scoped {
    ErrorOr<String> error <- ErrorOr:error("error message")
  } in ErrorOr<Int> value <- error.convertError()
  \ Testing.check<?>(value.getError().formatted(),"error message")
}
