/* -----------------------------------------------------------------------------
Copyright 2023 Kevin P. Barry

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----------------------------------------------------------------------------- */

// Author: Kevin P. Barry [ta0kira@gmail.com]

testcase "working tests" {
  success TestChecker
}

unittest test {
  // Set up parser.
  ZeoliteParseContext context <- ZeoliteParseContext.new()
      .include<ZeoliteWhitespace>()
      .include<ZeoliteLineComment>()
      .include<ZeoliteStringLiteral>()
      .include<ZeoliteTestcase>()
      .include<ZeoliteUpperSymbol>()
      .include<ZeoliteLowerSymbol>()
      .include<ZeoliteParamName>()
      .include<ZeoliteBraceSection>()

  String originalData <- "{Category #param}\"string\\n\" // done"
  $ReadOnly[originalData]$

  // Parse the input data.
  TextStream input <- TextStream.new(originalData)
  Vector<ZeoliteParsed> output <- Vector<ZeoliteParsed>.new()
  \ StreamTokenizer:new(context: context, tokenizer: context.defaultTokenizer()).tokenizeAll(input, output)

  // Expected parsed structure.
  DefaultOrder<ZeoliteParsed> expected <- Vector<ZeoliteParsed>.new()
      .append(ZeoliteParsed.section(label: "ZeoliteBraceSection", Vector<ZeoliteParsed>.new()
          .append(ZeoliteParsed.leaf(label: "ZeoliteOpenBrace",    content: "{"))
          .append(ZeoliteParsed.leaf(label: "ZeoliteCategoryName", content: "Category"))
          .append(ZeoliteParsed.leaf(label: "ZeoliteWhitespace",   content: " "))
          .append(ZeoliteParsed.leaf(label: "ZeoliteParamName",    content: "#param"))
          .append(ZeoliteParsed.leaf(label: "ZeoliteCloseBrace",   content: "}"))))
      .append(ZeoliteParsed.section(label: "ZeoliteStringLiteral", Vector<ZeoliteParsed>.new()
          .append(ZeoliteParsed.leaf(label: "ZeoliteDoubleQuote", content: "\""))
          .append(ZeoliteParsed.leaf(label: "ZeoliteQuotedChar",  content: "string"))
          .append(ZeoliteParsed.leaf(label: "ZeoliteEscapedChar", content: "\\n"))
          .append(ZeoliteParsed.leaf(label: "ZeoliteDoubleQuote", content: "\""))))
      .append(ZeoliteParsed.leaf(label: "ZeoliteWhitespace",  content: " "))
      .append(ZeoliteParsed.leaf(label: "ZeoliteLineComment", content: "// done"))

  // Check parsed data.
  \ input.atEnd() `Matches:with` CheckValue:equals(true)
  \ output `Matches:with` CheckSequence:matches(CheckSequence:using(expected))

  // Expected re-serialized data.
  [Append<Formatted> & Build<String>] builder <- String.builder()
  UnformattedFormatter formatter <- UnformattedFormatter.new()
  traverse (output.defaultOrder() -> ZeoliteParsed parsed) {
    \ builder.append(parsed.formatWith(formatter))
  }

  // Check basic re-serialization of parsed data.
  \ builder.build() `Matches:with` CheckValue:equals(originalData)
}
